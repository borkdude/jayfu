{:tasks
 {:requires ([babashka.fs :as fs]
             [clojure.string :as str])
  :init (def windows? (str/starts-with? (System/getProperty "os.name")
                                        "Windows"))
  run-main {:doc "Run main"
            :task (apply clojure "-M -m jayfu.main" *command-line-args*)}
  uberjar {:doc "Builds uberjar"
           :task (when (seq (fs/modified-since "jayfu.jar"
                                               ["deps.edn" "build.clj" "src"]))
                   (clojure "-T:build uber"))
           :override-builtin true}
  run-uber {:doc "Run uberjar"
            :depends [uberjar]
            :task (apply shell "java -jar jayfu.jar" *command-line-args*)}
  graalvm {:doc "Checks GRAALVM_HOME env var"
           :task
           (let [env (System/getenv "GRAALVM_HOME")]
             (assert env "Set GRAALVM_HOME")
             env)}
  native-image {:doc "Builds native image"
                :depends [graalvm uberjar]
                :task (do
                        (shell (str (fs/file graalvm
                                             "bin"
                                             (if windows?
                                               "gu.cmd"
                                               "gu")))
                               "install" "native-image")
                        (apply shell (str (fs/file graalvm
                                             "bin"
                                             (if windows?
                                               "native-image.cmd"
                                               "native-image")))
                               "-jar" "jayfu.jar"
                               "--no-fallback"
                               "-H:Name=jayfu"
                               *command-line-args*))}}}
